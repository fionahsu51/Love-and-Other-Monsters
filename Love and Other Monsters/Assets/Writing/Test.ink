#format:italic #portrait:none #bg:morgan_manor_front
Edith straightened her posture and hastily took a sip of her tea.

->the_girls

===the_girls===
#format:none #portrait:edith 
Certainly, miss. What do you wish to know?

*   [Dolores]
    #format:none #portrait:edith
    Oh, the poor dear has done some good for herself. She runs a shop in town these days. You know, your Father was ever attentive to her. 
    
    He offered to invest in her business but she refused— with graciousness and gratitude, I might add. 
    
    It’s unfortunate, but understandable. When one has nothing, one wishes to create their own something, or something like that.
    
    #format:italic #portrait:none
    I thought of Dolores standing behind a shop counter, looking wistfully out the window. I wondered, if during those dead hours, she ever thought of me and our childhood days together. 
    
    I was far overdue for a visit.
    
    #format: none #portrait: victoria
    May I ask something else, Edith?
    ->the_girls
*   [Freya]
     #format:none #portrait:edith
    Such a strong head on her shoulders, that girl! Ever since her father appointed her heir, she works diligently night and day towards her future duties. 
    
    I often see her on my walks into town swinging away at her rapier. Sometimes, even into the night. 
    
    She’s a very personable one, as well. Always seeing to the needs of the townsfolk, she reminds me of your Father in that way. 
    
    She has a temper, mind you. But I’m sure you recall that yourself.
    
    #format:italic #portrait:none
    It was true, Freya could be easily cross at times. I had hoped she would have grown out of it, but there was a sense of nostalgia to know she had not changed so much. 
    
    At least, I hoped so.

    #format: none #portrait: victoria
    If I may change the subject...
    ->the_girls

*   [Delia]
    #format:italic #portrait:none
    Edith’s eyes gave a twinge of sorrow and foreboding. She leaned forward, as if to hush my friend’s name from my lips.
    
    #format:none #portrait:edith
    Miss Victoria, I must warn you. There is talk in the town of some shadowy creature stalking the woods and churchyard at night. 
    
    With your Father gone, the night becomes more and more their domain– poor, wretched creatures. Of course they are not always as they seem, my dear. Your Father– bless him, knew the goodness in so many things. 
    
    But they are still dangerous, and you are not equipped to handle them as he was. You must take heed, my lady. 
    
    Promise me, you will do this, if not for me– then for your Father?
    
    #format:italic #portrait:none
    I was taken aback by her words, forgetting my question entirely. Monsters were always something to be feared, but it had not occurred to me that anyone would even think to come so close to Castelonia. 
    
    My father’s hold on the town was stronger than even I anticipated. It was too dreadful to think of– something mysterious and murderous at the very edges of our homes, it was best not to enquire further. 
    
    #format: none #portrait: victoria
    Perhaps you could tell me about something else.
    
    ->the_girls
*   ->

#format:italic #portrait:none
Suddenly, there was a knock on the door. Edith leapt in her seat.

#format:none #portrait:edith
I had not expected anyone to call upon the house today but you, miss. Do excuse me.

#format:italic #portrait:none
I watched Edith leave me alone in the sitting room and the crackling of the fire. I was content to wallow inside with Edith and my grief. Whoever it was, I had fully intended to dismiss them. 

Edith returned with an even more astonished look on her face, I stood up on the spot.

#format:none #portrait:edith
My lady, Miss Freya Hawthorne wishes to have an audience with you.

#format:italic #portrait:none
Freya! Freya, of all people, had brought herself to our door.

#format: none #portrait: victoria
By all means, please see her in.

#format:italic #portrait:none
I remained standing for a moment, unsure of how to conduct myself. I was a mixture of one part joy, one part anxiety, and another part trepidation. 

Would she truly be the same as she was in our childhood? Would our talks be as before? And what of Delia? I sat down and began sipping, no– gulping– the last of my tea. It was all I could think to do until I saw Freya again. 

I set my cup down and stood up at the sight of her. I heard the teacup clattering back into its plate before I could speak.











->END