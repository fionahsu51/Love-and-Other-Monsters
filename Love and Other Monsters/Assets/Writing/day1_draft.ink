INCLUDE globals.ink
-> day_1

=== day_1 ===

What if it eats you? #format:italic #bg: intro_carriage 

The ride to Castelonia is just as I remember it. Rolling hills and nothing else for miles on end on that winding dirt road connecting the town to the rest of the world. #format:none
Ten years ago, I looked out the carriage window through teary eyes, drifting in and out of sleep.


Every time the bumpy road jolted me awake, I would see those same hills, crawling by lazily, so indistinguishable from each other, it was like we were going nowhere at all.

I don’t sleep this time.



Here we go, miss. #portrait:driver #bg: victoria_outside_carriage

Thank you. #portrait:victoria

I walk up the stone path leading to the Hawthorne Estate. Claudius and Freya stand in front of the house, engrossed in conversation. #bg: hawthorne_driveway #portrait:none #speaker-cl:freya_neutral #speaker-cr:claudius_neutral #speaker-cl-transition:a #speaker-cr-transition:a

They mirror each other almost perfectly in posture—arms crossed, back straight, brows furrowed—though I notice Freya has managed to surpass even her father in height. 

Not terribly surprising, remembering how much I struggled to keep up with her long strides on those rare occasions when she was allowed to come play in Father’s yard. 

Freya's temperamental nature meant we were always at each other’s throats, jumping from one inconsequential fight to the next, though her sister Delia would intervene before anyone threw a punch. Usually.

Freya sees me first, eyes widening, and whispers something to Claudius, who whips around. #speaker-cl:freya_surprised

V-Victoria? What are you doing here? #portrait:claudius #speaker-cr:claudius_surprised

For a moment, Claudius looks like he’s seen a ghost. I shift uncertainly as he quickly regains his composure. #portrait:none

Forgive me. I only mean—you’re earlier than I expected. #portrait:claudius #speaker-cr:claudius_neutral

I nod, holding up the letter. #portrait:none

I left as soon as I heard. #portrait:victoria

Freya opens her mouth, then snaps it shut. She furtively glances at her father. An unreadable look passes between them. #portrait:none #speaker-cl:freya_reluctant

Ah. Well. Thank the stars for your swift and safe passage. #portrait:claudius #speaker-cr:claudius_smile

My sincere condolences for your loss, Victoria. #speaker-cl:freya_neutral #speaker-cr:claudius_neutral 

The angels themselves open heaven’s gates… for the most benevolent and caring.

… Thank you. #portrait:victoria

Claudius looks expectantly at Freya. #portrait:none #speaker-cl:freya_reluctant 

Um. Deepest condolences, Victoria. #portrait:freya #speaker-cl:freya_closed_eyes 

I give an awkward nod. The frantic mix of emotions that the letter brought—grief, shock, confusion, anger—has settled into a strange numbness, and I’d like to keep it that way for now. #portrait:none

Thankfully, Claudius, brisk as always, doesn’t dwell on the subject.

I’m sure much has changed since your departure. Freya, dearest, why don’t you take the carriage and show Victoria around the town? I... have some business to take care of. #portrait:claudius #speaker-cr:claudius_smile 

Uh... #portrait:freya #speaker-cl:freya_reluctant #speaker-cr:claudius_smile

Claudius raises his eyebrows at her and Freya huffs quietly. #portrait:none #speaker-cr:claudius_neutral

Of course. It would be my pleasure. #portrait:freya #speaker-cl:freya_smirk

I hold back a snort—it’s clear pleasure is the last thing on her mind. #portrait:none

I’m exhausted, thoughts too jumbled to hold a conversation, and the mere suggestion of another carriage ride makes my back groan in protest. 

But the thought of entering Father’s manor, seeing that old rocking chair sitting empty like a carcass... I have a feeling that rest wouldn’t come easily.

And for all her reluctance, something tells me Freya could use a moment away from her father’s watchful gaze.

Shall we? #portrait:freya #speaker-cl:freya_awkward 

She jerks her thumb towards the carriage. #portrait:none

I nod and follow Freya as Claudius turns back towards the house. #speaker-cl:default #speaker-c:freya_neutral #speaker-cr:default #speaker-c-transition:a

	Urgh— #portrait:victoria

I struggle to hoist my suitcase into the back of the carriage. Before I know it, Freya’s coming over and grabbing the other end. #portrait:none

(What should I do?)

*   [Let her help you.]

    I mean to do it together, but Freya effortlessly lifts the suitcase from my arms and into the carriage. Clearly her training has paid off. #portrait:none #speaker:freya_smirk
    
    Oh. Thanks. #portrait:victoria
    
    Uh huh. #portrait:freya #speaker:freya_blushing
    
    She jumps into the seat. The foot iron makes it easy to climb in, but I don’t protest when Freya reaches out a gloved hand to help me up. #portrait:none 
    
    -> start_tour
	
*   [Do it yourself.]

    I’ve got it, thanks. #portrait:victoria
    
    Freya shrugs. #portrait:none #speaker:freya_reluctant
    
    Suit yourself. #portrait:freya
    
    She lets go and I almost stumble backwards from the weight. I catch her smirking before she turns away. Goddamn it. #portrait:none #speaker:freya_surprised
    
    I try to appear unbothered when I finally slide into the seat next to Freya, though I’m sure she can hear how hard I’m breathing from the exertion. #speaker:freya_neutral
    
    -> start_tour

= start_tour

All set? #portrait:freya #speaker:freya_smirk

I nod, Freya flicks the reins, and we’re off. #portrait:none #speaker:freya_neutral

The beginnings of an evening breeze are picking up by the time we reach the town square. I lean back in my seat, half-listening, as Freya halfheartedly points out the various shops and stands lining the streets. #bg:town_square_day #speaker:default

It’s almost eerie, the way the townsfolk look both strange and familiar, as if all the faces have been swapped out from a familiar painting.

A little boy kicks his legs back and forth on the edge of the fountain, poking at the water when his parents aren’t looking, which seems to be most of the time. 

Across the street, a frail-looking young woman sits on the library steps, hand shielding her face from the setting sun, silhouette so reminiscent of Freya’s sister that I almost wave on instinct.

Claudius was wrong. This town hasn’t changed at all.

… library’s still standing, who knows how old that thing is… And there’s the flower shop, closes at sunset— #portrait:freya #speaker:freya_reluctant #speaker-transition:a

I turn to look and a pang of nostalgia hits me so swiftly, I almost miss what caused it: a bouquet of pale purple asters sitting on display in the shop window. #portrait:none #bg:flower_shop_asters

Wait—can we stop here? #portrait:victoria

Freya brings us to a halt and I rush into the shop, digging coins from my pocket. It’s stupid, really. Who knows if I’ll even see—if she’ll even want to— #portrait:none #speaker:default

But when the florist hands me the asters, my eyes water at the sweet scent and the rush of memories it carries. <i>Dolores</i>. Of course I won’t leave without a visit. Of course. 

Freya eyes the bouquet when I return to the carriage. #speaker:freya_uncertain #speaker-transition:a

*   [Explain.]
    
    It’s for, um. A friend. #portrait:victoria
    
    Freya’s lips quirk at my hesitation. #portrait:none #speaker:freya_smirk

    A friend, or a… <i>friend</i>? #portrait:freya #speaker:freya_surprised
    
    She raises her eyebrows suggestively. #portrait:none
    
    (What should I say?)
    
    **  ["It's not like that."]
        
        It’s not—it wasn’t like that. #portrait:victoria
        
        I laugh uncomfortably, then immediately feel guilty. #portrait:none
        
        I mean... #portrait:victoria
        
        I struggle to find the right words, but they won’t come. #portrait:none
        
        Forget it. You don’t have to tell me. #portrait:freya #speaker:freya_reluctant
        
        That’s not—oh, whatever. #portrait:victoria
        
        I sigh and turn away. Clearly Freya’s sour attitude has only grown stronger in my absence. Maybe ten years ago, this would have inspired us to start a toy sword battle, but now… Now, it just gives me a headache. #portrait:none
        
        -> horse_convo
        
    **  [“I don’t know.”]
    
        I—ah, well… #portrait:victoria
    
        I stammer, suddenly unsure. It wasn’t like that, at least not in the way Freya’s imagining it, I’m sure. #portrait:none
        
        But I remember the way it felt to see a new letter in my mailbox. The way I would carry the sealed envelopes around for days, as if opening them would break the spell. And then…
    
        I feel Freya’s eyes on me and realize I’ve been silent for too long.
    
    	I don’t know, it was… #portrait:victoria
    
        Safe. Magical. Temporary. #portrait:none
    
    	It was a long time ago. #portrait:victoria

    	Uh huh. #portrait:freya #speaker:freya_uncertain

        She gives me a sidelong glance, but doesn’t pry. I almost wish she would. #speaker:freya_neutral #portrait:none

        -> horse_convo
        
*   [Say nothing.]
        
    A question hangs in the air, but Freya doesn’t ask, so I don’t answer. 

    What is there to say? I've no idea how to explain the jumble of feelings I’ve been trying to unwind for the last two years. #speaker:freya_neutral
    
    And in any case, Freya doesn't seem much for conversation. Better keep my business to myself.
    
    -> horse_convo
    
= horse_convo

The sun floods the town in a bright orange glow as we ride back to the Hawthorne Estate. Freya clears her throat as we approach the house. #bg:hawthorne_driveway_sunset 

Your father’s horse is in our stable. We have an old carriage you can use while you’re here. If you’d like. #portrait:freya #speaker:freya_uncertain

(My father's horse…) #portrait:none

*   [Be playful.]

    The horse has a name, you know… #portrait:victoria
    
    I don’t have to look to know that Freya’s rolling her eyes. #portrait:none
    
    “Courage” is hardly a name for a horse. Couldn’t think of anything more… majestic? #portrait:freya #speaker:freya_reluctant
    
    Well, I was five. Big word for a five-year-old. #portrait:victoria
    
    I decide not to tell her that my first choice was “Spots”. #portrait:none
    
    What’d you name your horse? #portrait:victoria

    I didn’t. Father did. #portrait:freya #speaker:freya_neutral
    
    Of course. #portrait:none
    
    Well, what did <i>your father</i> name the horse, then? <i>King Henry VIII</i>? #portrait:victoria
    
    I sneak a glance at Freya just in time to see the corners of her mouth twitching. #portrait:none
    
    Phantom. #portrait:freya #speaker:freya_smirk

    As if on cue, Phantom gives a snort, and Freya laughs—a real laugh, so carefree I almost can’t believe it came from her. #portrait:none #speaker:freya_laugh
    
    See? She knows her name. #portrait:freya 
    
    Well, I guess Phantom’s not a terrible name for a horse… #portrait:victoria #speaker:freya_smirk
    
    Yeah, yeah. #portrait:freya
    
    -> end_tour
    
*   [Be polite.]

	That would be nice, thank you. #portrait:victoria 

    I remember mostly keeping a safe distance from Father’s horse as a child, wary of those big black eyes, especially after he sidestepped onto my foot and made it swell purple for a week. #portrait:none #speaker:freya_neutral

    Father, on the other hand, loved Courage dearly, often dramatically whispering secrets into his ear while grinning at me as I demanded to know what they were talking about. 

    I wouldn’t be surprised if that horse knows more about my father than I do.
    
    -> end_tour

= end_tour

Before long, Hawthorne Estate looms up ahead. Freya brings the carriage to a halt near the back of the property, next to the stable. #portrait:none


I hope Courage hasn’t been too much trouble. #portrait:victoria

Don’t mention it. He’s an angel. Though Phantom isn’t too happy about sharing her carrots. #portrait:freya #speaker:freya_neutral

I try to picture the horses eating out of Freya’s palm and smile at the thought, though something about the image doesn’t feel quite right… #portrait:none

Doesn’t your sister usually take care of the horses? Since you’re busy, uh, studying the blade and all… #portrait:victoria

There’s no reply. When I look over, she’s staring straight ahead in stony silence. What did I say? #portrait:none #speaker:freya_neutral

… Freya? #portrait:victoria

… #portrait:freya

Del—My sister— #speaker:freya_closed_eyes

Her voice wavers uncharacteristically and the realization hits me. #portrait:none

A coldness spreads in my chest. Delia… It’s true that she was never the picture of health when we were younger, but somehow I never imagined coming back to a town without her in it.

	I’m sorry, I—I had no idea… #portrait:victoria 

I scramble for something more meaningful to say, still reeling. #portrait:none

[What should I say?]

*   [Be sincere.]

    She was… I know you two were… #portrait:victoria
    
    Anything I could say feels empty, dishonest. In truth, I don’t know what kind of person Delia grew up to be, or whether she and Freya ever grew apart. #portrait:none
    Their bond seemed unshakable when we were kids, but I know too well how quickly things can change—a universe between two people, there and then gone. 
    
    And I know that no matter what place Delia had in Freya’s life, that spot will never be filled again. Words aren’t enough, but I say them anyway.
   
    Oh, Freya, I’m so sorry. #portrait:victoria
    
    Silence. And then, so quietly I almost miss it— #portrait:none
 
    I’m sorry, too. #portrait:freya #speaker:freya_upset
    	
    I don’t know if she’s referring to Delia or my father, but I nod. I wish I could offer something else—a hug, or even just a comforting hand on her shoulder. #portrait:none 
    
    But even as we sit side by side, Freya feels miles away, untouchable. So I keep my hands in my lap as we watch the sun disappear behind the hills, and when I see her in the corner of my vision, silently wiping at her eyes, I pretend not to notice.
    
    -> go_home

*   [Be courteous.]

	I’m sorry for your loss, Freya. #portrait:victoria

    I do mean it, but just like Freya’s earlier condolences, it comes out hollow. #portrait:none

	Yeah. Well. #portrait:freya #speaker:freya_upset
	
	She closes her eyes, swallowing hard. #portrait:none #speaker:freya_closed_eyes

	You should head back before it gets dark. #portrait:freya

	Right. Yeah. #portrait:victoria

    -> go_home

= go_home

There’s little light left in the sky when I finally pull up to my father’s manor in the borrowed carriage. Courage is skittish and stubborn, only barely listening to my commands, but he perks up at the familiar sight of the house. #portrait:none #bg: morgan_driveway_sunset #speaker:default

I wonder whether Courage knows his owner is gone forever, or if he’s expecting Father to come out and greet him at any moment.

I’m not sure which I believe more myself.

Crossing the threshold feels like walking into a dream. I’ve imagined this scene a million times: the creak of the door, the rush of warm air carrying the house’s signature scent—parchment, firewood, leather, cinnamon. 

But the house is cold when I enter, as cold as the outside air and twice as still. It smells like dust. 

Hello? #portrait:victoria

There’s no reply, of course. Still, for a second I almost hear it—the rocking chair groaning as Father rises from his seat by the fire, ready to envelop me in his arms. #portrait:none

… #portrait:victoria

I’m home, Father.

My throat tightens and I wonder if the tears will finally come. Instead, a bone-deep exhaustion carries me through the entrance hall, up the stairs, and into my old bedroom, where I collapse onto the bed. #portrait:none #bg:desk_night

For a moment I sit in the darkness, listening to the sound of my own breathing. The familiar scent of the room makes my head spin. 

Eventually I find the courage to light a lamp. Though largely bare, the room seems well-maintained. The bed is made and a vase of flowers sits on the desk—dry and wilted, but far too intact to have been there for years.

I’m suddenly reminded of my own bouquet still sitting in the carriage outside. 

Shit. #portrait:victoria

In my struggle to get Courage back home, I’ve almost forgotten. It’s late now, unreasonably late for a visit, but the thought of Dolores here, in the same town, merely a carriage ride away—my heart leaps. #portrait:none

I waver, torn between excuses to stay here and excuses to leave. No matter where I look, this town is full of ghosts—it’s only a matter of which I can bear to face first.

With that thought, I unfurl my map onto the desk. 

Dolores lives in a room behind her family’s candle shop. I should check the directions. 

(I need to get to the candle shop.) #map:map #bg:map

-> dolores_visit

= dolores_visit

I leave the carriage at the edge of the town square and start down the narrow path towards the workers’ colony. Nearing the outskirts of town, the homes begin to lose their color; a deep stillness rises. #bg:dolores_path

Thick rows of birch trees blot out the moonlight. Ever since the well dried up, their branches have grown wild and gnarled, like hands reaching out for a drop of rain. 

Should be around here… #portrait:victoria

I follow my instincts and find myself nearing a familiar group of houses. It must be here; I can feel it. And sure enough, a few more steps and I see it: the faintest glow of light peeking through a curtained window. Her window. #portrait:none

I slow my pace, doubt rushing back at full force. Ten years since I was here. And two years since her last letter. 
I run through a thousand apologies in my head, but they all fall short. If only I knew what I was apologizing for. What will I say to her? 


A sudden chill rips me from my thoughts. I freeze in place. What was that? There was no noise, no movement, and yet… I’m certain something has changed.

I spin in a slow circle, scanning the trees. The trees stare back. I glance back towards Dolores’s house, and—

A shadow blocks the path up ahead. That silhouette… Something about it makes my blood run cold.

It moves.

I dash behind the nearest tree, heart pounding in my ears. Did it see me? My breath comes out in gasps. Surely it can hear. I grip the trunk with sweaty palms and chance a glance down the path. 


Ah! #portrait:victoria #bg:aveline_day1_cg

For a split second, I see it—a pair of glowing eyes, staring straight at me. #portrait:none


Then just as quickly, the figure is gone. #bg:dolores_path

What—? #portrait:victoria

I blink. I’m sure I saw it. I’m sure of it… But how did it disappear so quickly? #portrait:none

My knees wobble as I step back onto the path. When I look back at Dolores’s window, it’s dark.

I pick up the asters from the dirt where they’ve fallen. The visit will have to wait—I’m in no state for conversation, head pounding as I struggle to make sense of what I saw. 

As I walk back down the path, I turn back one more time. The figure is still nowhere to be seen, but I can’t shake the feeling that I’m being watched. 

I break into a run, not slowing down until I reach the carriage.

-> END
